apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlin-android-extensions'
apply from: new File(projectDir.parentFile, 'kn_ext.gradle')

apply from: new File(projectDir.parentFile, 'android_lib.gradle')

def frameworkName = "kn_rocket"
def frameworkObjCPrefix = 'KN' // rocket

def business_module_name = ":kn_bussiness"


def configureFramework(framework) {
    configure(framework) {
        isStatic = true
        transitiveExport = true
    }
}

def iosTargets

kotlin {

    targets {

        iosX64("ios")
        fromPreset(presets.android, 'android')
        iosTargets = [ios]

        configure(iosTargets) {
            binaries.framework { frame ->
                baseName = frameworkName
                configureFramework(frame)
            }

            binaries {
                def defaultDebugFramework = findFramework('', DEBUG)
                if(defaultDebugFramework != null) {
                    configureFramework(defaultDebugFramework)
                }

                def defaultReleaseFramework = findFramework('', RELEASE)
                if(defaultReleaseFramework != null) {
                    configureFramework(defaultReleaseFramework)
                }
            }

            compilations.main {
                extraOpts '-Xobjc-generics'
                extraOpts '-module-name', frameworkObjCPrefix
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                api project(business_module_name)
            }
        }

        androidMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                api project(business_module_name)
            }
        }

        iosMain {
            dependencies {
                api project(business_module_name)
            }
        }
    }



}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}
